#!/usr/bin/env perl



use strict;
use warnings;

use File::Path qw(make_path);
use IO::File;
use AIngle::DataFile;
use AIngle::DataFileWriter;

my $schema = do {
    my $fh = IO::File->new('../../share/test/schemas/interop.ain');
    local $/ = undef;
    <$fh>
};

my $datum = {
    intField    => 12,
    longField   => 15234324,
    stringField => 'hey',
    boolField   => 1,
    floatField  => 1234.0,
    doubleField => -1234.0,
    bytesField  => '12312adf',
    nullField   => undef,
    arrayField  => [5.0, 0.0, 12.0],
    mapField    => {
        a   => { label => 'a'   },
        bee => { label => 'cee' },
    },
    unionField  => 12.0,
    enumField   => 'C',
    fixedField  => '1019181716151413',
    recordField => {
        label    => 'blah',
        children => [ { label => 'inner', children => [] } ]
    },
};

while (my ($codec, $enabled) = each(%AIngle::DataFile::ValidCodec)) {
    next unless $enabled;
    my $outdir = '../../build/interop/data';
    make_path($outdir);
    my $path = sprintf('>%s/perl%s.aingle', $outdir,
                       $codec eq 'null' ? '' : '_'.$codec);
    my $fh = IO::File->new($path);
    my $writer_schema = AIngle::Schema->parse($schema);
    my $writer = AIngle::DataFileWriter->new(
        fh => $fh,
        codec => $codec,
        writer_schema => $writer_schema
    );
    $writer->print($datum);
    $writer->close;
}
